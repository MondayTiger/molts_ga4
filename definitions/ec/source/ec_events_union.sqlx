/*
初期実行時（過去分のデータを取り込む時は、最後のWHERE句を変更（デフォルト：7日前～5日前 → 5日前まで）
*/
-- Config blocks
config {
    type: "view",
    schema: "df_molts_ga4_source",
    description: "GA4データソース 確定&未確定データ統合 EC用",
    tags: ["daily", "source","ec"]
}

with events AS(
    SELECT 
        event_date,
        event_timestamp,
        event_name,
        event_bundle_sequence_id,
        stream_id,
        platform,
        user_id,
        user_pseudo_id,
        user_first_touch_timestamp,
        event_params,
        user_properties,
        STRUCT(traffic_source.name, traffic_source.medium, traffic_source.source) AS traffic_source,
        STRUCT(collected_traffic_source.manual_campaign_id,collected_traffic_source.manual_campaign_name,collected_traffic_source.manual_source,collected_traffic_source.manual_medium,collected_traffic_source.manual_term,collected_traffic_source.manual_content,collected_traffic_source.manual_source_platform,collected_traffic_source.manual_creative_format,collected_traffic_source.manual_marketing_tactic,collected_traffic_source.gclid,collected_traffic_source.dclid,collected_traffic_source.srsltid) AS collected_traffic_source,
        STRUCT(session_traffic_source_last_click.manual_campaign,session_traffic_source_last_click.google_ads_campaign) AS session_traffic_source_last_click,
        STRUCT(device.category, device.mobile_brand_name, device.mobile_model_name, device.mobile_marketing_name, device.mobile_os_hardware_model, device.operating_system, device.operating_system_version, device.vendor_id, device.advertising_id, device.language, device.is_limited_ad_tracking,device.time_zone_offset_seconds,device.browser,device.browser_version,
STRUCT(device.web_info.browser,device.web_info.browser_version,device.web_info.hostname) AS web_info) AS device,
        STRUCT(geo.city,geo.country,geo.continent,geo.region,geo.sub_continent,geo.metro) AS geo,
        STRUCT(privacy_info.analytics_storage,privacy_info.ads_storage,privacy_info.uses_transient_token) AS privacy_info, 
        STRUCT(ecommerce.total_item_quantity,ecommerce.purchase_revenue_in_usd,ecommerce.purchase_revenue,ecommerce.refund_value_in_usd,ecommerce.refund_value,ecommerce.shipping_value_in_usd,ecommerce.shipping_value,ecommerce.tax_value_in_usd,ecommerce.tax_value,ecommerce.unique_items,ecommerce.transaction_id) AS ecommerce,
        items,
        batch_page_id,
        batch_ordering_id,
        batch_event_index,
    FROM                    
        ${ref(constants.GA4_TABLE)}
    WHERE
        _table_suffix BETWEEN FORMAT_DATE("%Y%m%d", ${constants.GA4_EVENTS_START_DATE}) AND FORMAT_DATE("%Y%m%d",  ${constants.GA4_EVENTS_END_DATE})
        -- _table_suffix BETWEEN FORMAT_DATE("%Y%m%d", DATE_SUB(CURRENT_DATE('Asia/Tokyo'), INTERVAL 7 DAY)) AND FORMAT_DATE("%Y%m%d", DATE_SUB(CURRENT_DATE('Asia/Tokyo'), INTERVAL 1 DAY))
    --    _table_suffix BETWEEN '20201104' AND FORMAT_DATE("%Y%m%d", DATE_SUB(CURRENT_DATE('Asia/Tokyo'), INTERVAL 5 DAY))  -- 初回実行時に実施
),
intraday AS(
    SELECT 
        event_date,
        event_timestamp,
        event_name,
        event_bundle_sequence_id,
        stream_id,
        platform,
        user_id,
        user_pseudo_id,
        user_first_touch_timestamp,
        event_params,
        user_properties,
        STRUCT(traffic_source.name, traffic_source.medium, traffic_source.source) AS traffic_source,
        STRUCT(collected_traffic_source.manual_campaign_id,collected_traffic_source.manual_campaign_name,collected_traffic_source.manual_source,collected_traffic_source.manual_medium,collected_traffic_source.manual_term,collected_traffic_source.manual_content,collected_traffic_source.manual_source_platform,collected_traffic_source.manual_creative_format,collected_traffic_source.manual_marketing_tactic,collected_traffic_source.gclid,collected_traffic_source.dclid,collected_traffic_source.srsltid) AS collected_traffic_source,
        STRUCT(session_traffic_source_last_click.manual_campaign,session_traffic_source_last_click.google_ads_campaign) AS session_traffic_source_last_click,
        STRUCT(device.category, device.mobile_brand_name, device.mobile_model_name, device.mobile_marketing_name, device.mobile_os_hardware_model, device.operating_system, device.operating_system_version, device.vendor_id, device.advertising_id, device.language, device.is_limited_ad_tracking,device.time_zone_offset_seconds,device.browser,device.browser_version,
STRUCT(device.web_info.browser,device.web_info.browser_version,device.web_info.hostname) AS web_info) AS device,
        STRUCT(geo.city,geo.country,geo.continent,geo.region,geo.sub_continent,geo.metro) AS geo,
        STRUCT(privacy_info.analytics_storage,privacy_info.ads_storage,privacy_info.uses_transient_token) AS privacy_info, 
        STRUCT(ecommerce.total_item_quantity,ecommerce.purchase_revenue_in_usd,ecommerce.purchase_revenue,ecommerce.refund_value_in_usd,ecommerce.refund_value,ecommerce.shipping_value_in_usd,ecommerce.shipping_value,ecommerce.tax_value_in_usd,ecommerce.tax_value,ecommerce.unique_items,ecommerce.transaction_id) AS ecommerce,
        items,
        batch_page_id,
        batch_ordering_id,
        batch_event_index,
    FROM                    
        ${ref(constants.GA4_INTRADAY_TABLE)}
    WHERE
        _table_suffix BETWEEN
            (SELECT FORMAT_DATE("%Y%m%d",PARSE_DATE("%Y%m%d", MAX(event_date)) + 1) FROM events)
            AND FORMAT_DATE("%Y%m%d",(CURRENT_DATE('Asia/Tokyo')))
),
ga_union AS(
    SELECT *
    FROM events
    UNION ALL
    SELECT *
    FROM intraday
),
ga AS(
SELECT

    -- timestamp
    event_date,
    event_timestamp,

    -- event
    event_name,
    event_bundle_sequence_id,

    -- user
    COALESCE(user_id, ${helpers.getUserPropertyNoColumnName('user_id','string')}, ${helpers.getEventParamNoColumnName('user_id','string')}) AS user_id,
    user_pseudo_id,

    -- session
    ${helpers.getEventParamAll('ga_session_id','string')},
    ${helpers.getEventParam('ga_session_number','int')},

    -- page
    device.web_info.hostname,
    ${helpers.getEventParam('page_location')},
    ${helpers.getEventParamAll('page_title','string')},
    COALESCE(${helpers.getEventParamNoColumnName('firebase_screen_class')}, ${helpers.getEventParamNoColumnName('screen_class')}) AS screen_class,
    COALESCE(${helpers.getEventParamNoColumnName('firebase_screen')}, ${helpers.getEventParamNoColumnName('screen_name')}) AS screen_name,
    
    -- engagement_time
    ${helpers.getEventParam('engagement_time_msec','int')},

    -- traffic_type (excluding IP address)
    ${helpers.getEventParamAll('traffic_type','string')},

    -- debug_mode
    ${helpers.getEventParam('debug_mode')},
    ${helpers.getEventParam('debug_event')},

    -- batch
    COALESCE(batch_page_id, ${helpers.getEventParamNoColumnName('batch_page_id','int')}) AS batch_page_id,
    COALESCE(batch_ordering_id, ${helpers.getEventParamNoColumnName('batch_ordering_id','int')}) AS batch_ordering_id,
    COALESCE(batch_event_index, ${helpers.getEventParamNoColumnName('batch_event_index','int')}) AS batch_event_index,

    -- common
    ${helpers.getEventParamAll('value','double')},
    ${helpers.getEventParamAll('method','string')}, 

    -- view_promotion, select_promotion    
    COALESCE(${helpers.getEventParamNoColumnName('promotion_id','string')}, (SELECT items[SAFE_OFFSET(0)].promotion_id)) AS promotion_id,
    COALESCE(${helpers.getEventParamNoColumnName('promotion_name','string')}, (SELECT items[SAFE_OFFSET(0)].promotion_name)) AS promotion_name,
    COALESCE(${helpers.getEventParamNoColumnName('creative_slot','string')}, (SELECT items[SAFE_OFFSET(0)].creative_slot)) AS creative_slot,
    COALESCE(${helpers.getEventParamNoColumnName('creative_name','string')}, (SELECT items[SAFE_OFFSET(0)].creative_name)) AS creative_name,

    -- ecommerce
    ecommerce.transaction_id AS transaction_id,
    COALESCE((SELECT items[SAFE_OFFSET(0)].item_id),'item_id') AS item_id,
    COALESCE(${helpers.getEventParamNoColumnName('item_list_id','string')},(SELECT items[SAFE_OFFSET(0)].item_list_id),'item_list_id') AS item_list_id,
    COALESCE(${helpers.getEventParamNoColumnName('item_list_name','string')}, (SELECT items[SAFE_OFFSET(0)].item_list_name),'item_list_name') AS item_list_name,

    -- ecommerce
    ecommerce,
    items,

    -- event_parameter
    -- ${helpers.getEventParamAll('event_parameter_name','string')},
    -- ${helpers.getEventParamAll('event_parameter_name','int')},
    -- ${helpers.getEventParamAll('event_parameter_name','double')},

    -- user_property
    -- ${helpers.getUserPropertyAll('user_property_name','string')},
    -- ${helpers.getUserPropertyAll('user_property_name','int')},
    -- ${helpers.getUserPropertyAll('user_property_name','double')},

FROM                    
	ga_union
)
-- unique_key：イベントを一意に定めるためのカラム※GAのバグやイベントの重複発火などにより全く同じイベントが発生した場合は同一の値となってしまいます（ですがm_ga4_eventテーブルではevent_indexと合わせてに使うことで一意になります）
SELECT *,
    CONCAT(user_pseudo_id, ga_session_id, event_timestamp, COALESCE(batch_page_id,-1),COALESCE(batch_ordering_id,-1),COALESCE(batch_event_index,-1), event_name,
    CONCAT(COALESCE(page_location, screen_class,'page_location'),COALESCE(page_title,screen_name,'page_title')), COALESCE(engagement_time_msec,0),
    COALESCE(
        CASE 
        WHEN event_name IN ('purcahse','refund') THEN transaction_id
        WHEN event_name LIKE '%_promotion' THEN CONCAT(promotion_name, promotion_id, creative_slot, creative_name) -- view_promotion, select_promotionなど
        WHEN event_name IN ('view_item','select_item') THEN item_id
        WHEN event_name LIKE 'view_item_list' THEN CONCAT(item_list_id, item_list_name, item_id)
        WHEN event_name IN ('add_to_cart','remove_from_cart','add_to_wishlist') THEN item_id
        WHEN event_name IN ('begin_checkout','add_payment_info','add_shipping_info','view_cart') THEN SAFE_CAST(COALESCE(value,engagement_time_msec,0) AS STRING)
        -- 他にイベントがある場合はここに追加
        -- 例： WHEN event_name IN ('new_event') THEN event_parameter_name -- event_parameter_name はnew_eventイベントで使用されるパラメータ名を使用 ※218行目より上でevent_parameter_nameを作成する必要あり
        ELSE SAFE_CAST(COALESCE(engagement_time_msec,0) AS STRING)
        END, 'key') 
  ) AS unique_key
FROM ga
WHERE event_name IN ('add_payment_info',
'add_shipping_info',
'add_to_cart',
'add_to_wishlist',
'begin_checkout',
'purchase',
'refund',
'remove_from_cart',
'select_item',
'select_promotion',
'view_cart',
'view_item',
'view_item_list',
'view_promotion'
)