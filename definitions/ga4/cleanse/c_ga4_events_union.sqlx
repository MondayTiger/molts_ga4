-- Config blocks
config {
    type: "view",
    database: constants.PROJECT,
    schema: constants.CLEANSE,
    description: "GA4クレンジングデータ 統合データ",
    tags: ["daily", "cleanse"]
}


-- Query blocks
SELECT

    -- timestamp
    PARSE_DATE("%Y%m%d", event_date) AS event_date,
    event_timestamp,
    SAFE_CAST(FORMAT_TIMESTAMP('%Y-%m-%d %H:%M:%S', TIMESTAMP_MICROS(event_timestamp), 'Asia/Tokyo') AS DATETIME) AS event_timestamp_jst,

    -- event
    event_name,
    event_bundle_sequence_id,

    -- stream / platform
    stream_id,
    platform,

    -- user
    user_id,
    user_pseudo_id,
    user_first_touch_timestamp,

    -- user_acquistion_channel
    user_traffic_source,
    user_traffic_medium,
    user_traffic_campaign,

    -- session
    ga_session_id,
    ga_session_number,
    session_engaged,
    engaged_session_event,
    
    -- event_acquisition_channel
    event_traffic_source,
    event_traffic_medium,
    event_traffic_campaign,
    event_traffic_campaign_id, -- utm_id
    event_traffic_content,
    event_traffic_term,
    COALESCE(event_traffic_source_platform,${helpers.getQueryStringNoColumnName('page_location','utm_source_platform')}) AS event_traffic_source_platform,
    COALESCE(event_traffic_creative_format,${helpers.getQueryStringNoColumnName('page_location','utm_creative_format')}) AS event_traffic_creative_format,
    COALESCE(event_traffic_marketing_tactic,${helpers.getQueryStringNoColumnName('page_location','utm_marketing_tactic')}) AS event_traffic_marketing_tactic,
    event_traffic_gclid,
/*    ${helpers.getQueryString('page_location','utm_source_platform', 'event_traffic_source_platform')}, -- ep化したら修正
    ${helpers.getQueryString('page_location','utm_source_platform', 'event_traffic_creative_format')}, -- ep化したら修正
    ${helpers.getQueryString('page_location','utm_marketing_tactic', 'event_traffic_marketing_tactic')}, -- ep化したら修正
*/

    collected_traffic_source,
    session_traffic_source_last_click,

    -- device
	device_category,
	device_mobile_brand_name,
	device_mobile_model_name,
	device_mobile_marketing_name,
	device_mobile_os_hardware_model,
    operating_system,
    operating_system_version,
	device_vendor_id,
	device_advertising_id,
    language,
	is_limited_ad_tracking,
	time_zone_offset_seconds,
    browser,
	browser_version,

    -- geo
	geo_continent,
	geo_sub_continent,
	geo_country,
	geo_region,
	geo_metro,
	geo_city,

    -- privacy_info
	privacy_info_analytics_storage,
	privacy_info_ads_storage,
	privacy_info_uses_transient_token,

    -- page
    hostname,
    page_location,
    ${helpers.deleteQueryString('page_location','page_location_canonicalize')},
    content_group,
    page_title,
    entrances,
    flg_reload, -- original
    screen_class,
    screen_name,

    -- referrer
    page_referrer,

    -- scroll
    percent_scrolled,

    -- event_tracking (Click Tracking)
    event_category,
    event_action,
    event_label,
    link_id,
    link_classes,
    link_domain,
    link_url,
    ${helpers.deleteQueryString('link_url','link_url_canonicalize')},
    link_text,
    outbound,
    file_extension,
    file_name,
    value,
    method,

    -- video
    video_title,
    video_provider,
    video_url,
    visible,
    video_current_time,
    video_duration,
    video_percent,

    -- search_results
    search_term,

    -- experiment (a/b testing)
    experiment_id,
    variant_id,

    -- engagement_time
    engagement_time_msec,

    -- traffic_type (excluding IP address)
    traffic_type,

    -- debug_mode
    debug_mode,
    debug_event,

    -- add_column
    is_fixed_data,

    -- event_parameter clarity
    claritydimension,

    -- event_parameter article
    estimate_read_time,
    estimate_read_time_percent,

    -- event_parameter core web vitals
    cwv_id,
    cwv_value,
    cwv_delta,

    -- ecommerce
    ecommerce,
    items,

    -- event order
    batch_page_id,
    batch_ordering_id,
    batch_event_index,

    unique_key,

    -- event_parameter
    ${helpers.getCustomEventParamKeyAll()} 

    -- user_property
    ${helpers.getCustomUserPropertyKeyAll()}  


FROM                    
	${ref("ga4_events_union")}