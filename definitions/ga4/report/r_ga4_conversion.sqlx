-- Config blocks
config {
    //typeは初期構築時は"table"、増分更新時は"incremental"とする。初期構築時の作業手順はREADME.mdを要参照
    type: constants.MART_TYPE,
    bigquery: {
        // パーティションとクラスタリングを設定
        partitionBy: "event_date",
        clusterBy: ["user_pseudo_id", "ga_session_id", "event_name"]
    },
    database: constants.PROJECT,
    schema: constants.REPORT,
    description: "GA4マートデータ　イベントデータ用",
    dependencies: ["r_ga4_conversion_delete_unfixed"],
    tags: ["daily", "report","workflow"],
    columns: {
        // timestamp
        event_date: "日付",
        event_timestamp: "タイムスタンプ（マイクロ秒）",
        event_timestamp_jst: "タイムスタンプ_日本時間",
        entrance_timestamp: "タイムスタンプ（セッション開始）（マイクロ秒）",
        entrance_timestamp_jst: "タイムスタンプ（セッション開始）_日本時間",
        exit_timestamp: "タイムスタンプ（離脱）（マイクロ秒）",
        exit_timestamp_jst: "タイムスタンプ（離脱）_日本時間",

        // event
        event_name: "イベント名",
        event_bundle_sequence_id: "これらのイベントをまとめたアップロード用バンドルのシーケンシャル ID",

        // stream / platform
        stream_id: "ストリームID",
        platform: "プラットフォーム",

        // user
        user_id: "ユーザーID",
        user_pseudo_id: "ユニークユーザ識別用ID",
        user_first_touch_timestamp: "ユーザーが初めてアプリを起動したか、サイトに訪れた時刻（マイクロ秒単位）",

        // user_acquistion_channel
        user_traffic_source: "集客：ユーザー獲得参照元",
        user_traffic_medium: "集客：ユーザー獲得メディア",
        user_traffic_campaign: "集客：ユーザー獲得キャンペーン",

        // session
        ga_session_id: "セッションID",
        ga_session_number: "通算セッション数",
        session_duration: "セッション時間（秒）",
        session_total_engagement_time: "セッション合計エンゲージメント時間（秒）",
        session_total_page_view: "セッション合計ページビュー",
        session_total_screen_view: "セッション合計スクリーンビュー",
        is_bounce_no_transition: "直帰_遷移なし",
        is_bounce_no_engagement: "直帰_エンゲージメントなし",

        // session_acquisition_channel
        session_traffic_source: "集客：セッションの参照元",
        session_traffic_medium: "集客：セッションのメディア",
        session_traffic_campaign: "集客：セッションのキャンペーン",
        session_traffic_content: "集客：セッションの広告のコンテンツ",
        session_traffic_term: "集客：セッションの検索キーワード",
        session_traffic_source_platform: "集客：セッションの参照元プラットフォーム",
        session_traffic_creative_format: "集客：セッションのクリエイティブのタイプ",
        session_traffic_marketing_tactic: "集客：セッションのターゲティング条件",
        session_traffic_campaign_id: "集客：セッションのキャンペーンID（utm_id）",
        session_traffic_gclid: "集客：セッションのgclid",

        // device
        device_category: "デバイス カテゴリ",
        device_mobile_brand_name: "デバイスのブランド名",
        device_mobile_model_name: "デバイスのモデル名",
        device_mobile_marketing_name: "デバイスのマーケティング名",
        device_mobile_os_hardware_model: "オペレーティング システムから直接取得したデバイスのモデル情報",
        operating_system: "OS",
        operating_system_version: "OSのバージョン",
        device_vendor_id: "IDFV（IDFA を収集していない場合にのみ使用）",
        device_advertising_id: "広告 ID または IDFA",
        language: "言語",
        is_limited_ad_tracking: "デバイスの広告トラッキング制限の設定 iOS14 以降では、IDFA がゼロ以外の場合、false が返される",
        time_zone_offset_seconds: "GMT との時差（秒単位）",
        browser: "ブラウザ",
        browser_version: "ブラウザのバージョン",

        // geo
        geo_continent: "大陸",
        geo_sub_continent: "亜大陸",
        geo_country: "国",
        geo_region: "地域",
        geo_metro: "大都市圏",
        geo_city: "市区町村",

        // page
        hostname: "ホスト名",
        page_location: "URL",
        page_location_canonicalize: "正規化URL",
        content_group: "コンテンツグループ",
        page_title: "ページタイトル",
        entrances: "閲覧開始フラグ(GA4の entrances イベントパラメータ)",
        entrance_page: "セッション内の閲覧開始ページ",
        exit_page: "セッション内の離脱ページ",
        flg_reload: "リロードフラグ",

        // referrer
        page_referrer: "リファラー",

        // add_column
        is_fixed_data: "FIXしたデータか",

        // event_parameter clarity
        claritydimension: "Microsoft ClarityのURL",

        // event_parameter article
        estimate_read_time: "推定読了時間",
        estimate_read_time_percent: "読了割合",

        // event_parameter core web vitals
        cwv_id: "Core Web VitalsのID",
        cwv_value: "Core Web Vitalsのvalue",
        cwv_delta: "Core Web Vitalsのdelta",

        // session_traffic_source_last_clickの参照元
        session_traffic_source_last_click:{
            description: "集客：セッションの参照元情報",
            columns:{
                manual_campaign: {
                    description: "Description of the manual_campaign RECORD",
                    columns: {
                        manual_campaign: "集客：セッションのキャンペーン",
                        campaign_id: "集客：セッションのキャンペーンID（utm_id）",
                        campaign_name: "集客：セッションのキャンペーン",
                        source: "集客：セッションの参照元",
                        medium:"集客：セッションのメディア",
                        term: "集客：セッションの検索キーワード",
                        content: "集客：セッションの広告のコンテンツ",
                        source_platform: "集客：セッションの参照元プラットフォーム",
                        creative_format: "集客：セッションのクリエイティブのタイプ",
                        marketing_tactic: "集客：セッションのターゲティング条件"
                    }
                },
                google_ads_campaign: {
                    description: "Google広告関連",
                    columns: {
                        customer_id: "Google 広告アカウントに関連付けられた顧客 ID",
                        account_name: "Google広告のアカウント名",
                        campaign_id: "Google広告キャンペーンID",
                        campaign_name: "Google広告キャンペーン名",
                        ad_group_id: "Google広告キャンペーン内の広告グループのID",
                        ad_group_name: "Google広告キャンペーン内の広告グループ名"
                    } 
                }
            }
        },
        // event index
        batch_page_id: "バッチの発生ページ順",
        batch_ordering_id: "バッチの発生順番",
        batch_event_index: "イベントの順番",        
        unique_key: "イベントのユニーク化",
        event_index: "イベント順番（日付＋4桁の番号）※直近の日付で日をまたいだセッションのイベントについては番号が前日から連番になるので、日付+0001で始まらない場合がある"
    }
}


-- Query blocks
SELECT
    * 
FROM
    ${ref("m_ga4_event")}
WHERE
    (
        page_location LIKE '${constants.CV_PAGE_LOCATION_LIKE}' -- 'https://moltsinc.co.jp/%thanks%'
        OR page_location IN (${constants.CV_PAGE_LOCATIONS}) 
    )
    AND event_name IN ('page_view','screen_view')
    ${ when(incremental(), `AND event_date > (SELECT COALESCE(MAX(event_date),'${constants.GA4_FIRST_DATE}') FROM ${self()})`)} 
    ${ when(incremental(), ` AND event_date >= ${constants.GA4_EVENTS_START_DATE}`)}  -- incrementalで過去10日よりも古いデータを追加したい場合はこの行をコメントアウト




