/*
ページ分析ビュー
page_views: ページビュー数
unique_page_views: ページ別訪問数（ベージを訪問したセッション数）
sum_page_visit_time_msec: ページ別合計滞在時間（ミリ秒）※平均滞在時間は sum_page_visit_time_msec/page_viewsで算出。
sum_engagement_time_msec: ページ別合計エンゲージメント時間（ミリ秒）※平均時間は sum_engagement_time_msec/page_viewsで算出。
attributeEvents_sign_up:各セッション内でsign_upイベントが初回発生するまでにイベントが発生したイベント数。同一セッション内で複数回閲覧しても１としてカウントする。（対象CV：各CVの最初のCVまで。CVに到達するまでの全ページに+1（複数回ページビューがあっても1））
対象イベント：sign_up、generate_leadなど ※constants.jsにて設定
*/

-- Config blocks
config {
  type: "view",
  database: constants.PROJECT,
  schema: constants.REPORT,
  description: "GA4ページ分析ビュー。ページ滞在時間、ページ別エンゲージメント時間、ページ別CV貢献：各セッションの最初のページから各CVのセッション内最初のCVまでが対象。同一セッション内で複数回閲覧しても１としてカウントする。（対象CV：各CVの最初のCVまで。CVに到達するまでの全ページに+1（複数回ページビューがあっても1））",
  tags: ["report", "analysis"],
  columns: {
    page_location_canonicalize: "正規化URL",
    hostname: "ホスト名",
    event_date: "日付",
    page_views:"ページビュー数",
    unique_page_views:"ページ別訪問数（ベージを訪問したセッション数）",
    sum_page_visit_time_msec:"ページ別合計滞在時間（ミリ秒）",
    sum_engagement_time_msec:" ページ別合計エンゲージメント時間（ミリ秒）",
    attributeEvents_generate_lead:"各セッション内でgenerate_leadイベントが初回発生するまでにページ訪問したセッション数。同一セッション内で複数回訪問しても1としてカウント。",
    attributeEvents_sign_up:"各セッション内でsign_upイベントが初回発生するまでにページ訪問したセッション数。同一セッション内で複数回訪問しても1としてカウント。",
    attributeEvents_download_form:"各セッション内でdownload_formイベントが初回発生するまでにページ訪問したセッション数。同一セッション内で複数回訪問しても1としてカウント。",
    attributeEvents_file_download:"各セッション内でfile_downloadイベントが初回発生するまでにページ訪問したセッション数。同一セッション内で複数回訪問しても1としてカウント。",
    user_traffic_source: "集客：ユーザー獲得参照元",
    user_traffic_medium: "集客：ユーザー獲得メディア",
    user_traffic_campaign: "集客：ユーザー獲得キャンペーン",
    session_traffic_source: "集客：セッションの参照元",
    session_traffic_medium: "集客：セッションのメディア",
    session_traffic_campaign: "集客：セッションのキャンペーン",
    session_traffic_campaign_id: "集客：セッションのキャンペーンID（utm_id）",
    session_traffic_content: "集客：セッションの広告のコンテンツ",
    session_traffic_term: "集客：セッションの検索キーワード",
    session_traffic_source_platform: "集客：セッションの参照元プラットフォーム",
    session_traffic_creative_format: "集客：セッションのクリエイティブのタイプ",
    session_traffic_marketing_tactic: "集客：セッションのターゲティング条件",
    platform: "プラットフォーム",
    device_category: "デバイス カテゴリ",
    operating_system: "OS",
    browser: "ブラウザ",
    session_traffic_source_last_click: {
      description: "集客：セッションの参照元情報",
      columns: {
        manual_campaign: {
          description: "集客：セッションの参照元情報",
          columns: {
            manual_campaign: "集客：セッションのキャンペーン",
            campaign_id: "集客：セッションのキャンペーンID（utm_id）",
            campaign_name: "集客：セッションのキャンペーン",
            source: "集客：セッションの参照元",
            medium: "集客：セッションのメディア",
            term: "集客：セッションの検索キーワード",
            content: "集客：セッションの広告のコンテンツ",
            source_platform: "集客：セッションの参照元プラットフォーム",
            creative_format: "集客：セッションのクリエイティブのタイプ",
            marketing_tactic: "集客：セッションのターゲティング条件"
          }
        },
        google_ads_campaign: {
          description: "Google広告関連",
          columns: {
            customer_id: "Google 広告アカウントに関連付けられた顧客 ID",
            account_name: "Google広告のアカウント名",
            campaign_id: "Google広告キャンペーンID",
            campaign_name: "Google広告キャンペーン名",
            ad_group_id: "Google広告キャンペーン内の広告グループのID",
            ad_group_name: "Google広告キャンペーン内の広告グループ名"
          }
        }
      }
    }
  }
}


-- Query blocks

with date_range AS(
  SELECT
    ${constants.GA4_ANALYSIS_CV_START_DATE} AS start_date,
    ${constants.GA4_ANALYSIS_CV_END_DATE} AS end_date
),
s AS(
	SELECT DISTINCT
	event_date,
	user_pseudo_id,
	ga_session_id,	
	-- user_id,
	-- stream_id,
	user_traffic_source,
	user_traffic_medium,
	user_traffic_campaign,
--	ga_session_number,
	session_traffic_source,
	session_traffic_medium,
	session_traffic_campaign,
	session_traffic_campaign_id,
	session_traffic_content,
	session_traffic_term,
	session_traffic_source_platform,
	session_traffic_creative_format,
	session_traffic_marketing_tactic,
	platform,
	device_category,
	-- device_mobile_brand_name,
	-- device_mobile_model_name,
	-- device_mobile_marketing_name,
	-- device_mobile_os_hardware_model,
	operating_system,
	-- operating_system_version,
	-- device_vendor_id,
	-- device_advertising_id,
	-- language,
	browser,
	-- browser_version,
	-- geo_continent,
	-- geo_country,
	-- geo_region,
	-- geo_city,
	-- geo_sub_continent,
	-- geo_metro,
	session_traffic_source_last_click,
	FROM ${ref("m_ga4_session")}
  WHERE event_date BETWEEN (SELECT start_date FROM date_range) AND (SELECT end_date FROM date_range)  
	QUALIFY ROW_NUMBER()OVER(PARTITION BY event_date, user_pseudo_id,	ga_session_id ORDER BY entrance_timestamp) = 1 
),
e AS(
 SELECT DISTINCT 
	event_date,
	user_pseudo_id,
	ga_session_id,

	hostname,
	page_location_canonicalize,
	event_timestamp,
	event_timestamp_jst,
	event_name, 
	engagement_time_msec,
	COALESCE(batch_page_id,0) AS batch_page_id,
	COALESCE(batch_ordering_id, 0) AS batch_ordering_id,
	COALESCE(batch_event_index,0) AS batch_event_index,
	ROW_NUMBER()OVER(PARTITION BY user_pseudo_id, ga_session_id ORDER BY COALESCE(batch_page_id,0),COALESCE(batch_ordering_id, 0),event_timestamp, COALESCE(batch_event_index,0), IF(event_name IN (${constants.GA4_ANALYSIS_CV_EVENTS.map(value => `'${value}'`).join(", ")}),2,1)) AS rn

  -- 0 AS batch_page_id,
  -- 0 AS batch_ordering_id,
  -- 0 AS batch_event_index,
	-- 	ROW_NUMBER()OVER(PARTITION BY user_pseudo_id, ga_session_id ORDER BY event_timestamp,CASE WHEN event_name='purchase'THEN 3 WHEN event_name IN (${constants.GA4_ANALYSIS_CV_EVENTS.map(value => `'${value}'`).join(", ")}) THEN 2 ELSE 1 END) AS rn
  FROM ${ref("m_ga4_event")}
  WHERE event_date BETWEEN (SELECT start_date FROM date_range) AND (SELECT end_date FROM date_range)  
),
raws AS(
  SELECT *,
--    CASE WHEN event_name IN ('page_view','screen_view') THEN 1 ELSE 2 END AS page_event_number,
    LAG(page_location_canonicalize)OVER(PARTITION BY event_date, user_pseudo_id, ga_session_id ORDER BY batch_page_id,batch_ordering_id, event_timestamp, batch_event_index) AS prev_page_location_canonicalize
  FROM e 
),
agg AS(
SELECT *,
CASE WHEN prev_page_location_canonicalize = page_location_canonicalize THEN FALSE ELSE TRUE END AS new_page,
--SUM(CASE WHEN prev_page_location_canonicalize != page_location_canonicalize THEN 0 ELSE 1 END)OVER(PARTITION BY user_pseudo_id, ga_session_id ORDER BY batch_page_id,batch_ordering_id,event_timestamp, batch_event_index) AS flag_new_page
FROM raws
),
ordering AS(
  SELECT *,
  IF(new_page = TRUE, ROW_NUMBER()OVER(PARTITION BY event_date, user_pseudo_id,ga_session_id, new_page ORDER BY batch_page_id,batch_ordering_id,event_timestamp, batch_event_index),NULL) AS page_number,
  FROM agg a 
),
label AS(
  SELECT * EXCEPT(page_number),
  LAST_VALUE(page_number IGNORE NULLS)OVER(PARTITION BY event_date, user_pseudo_id,ga_session_id ORDER BY batch_page_id,batch_ordering_id,event_timestamp, batch_event_index) AS page_number
  FROM ordering o
),
visit_time AS(
  SELECT event_date, user_pseudo_id, ga_session_id, page_location_canonicalize, page_number,
  TIMESTAMP_DIFF(MAX(TIMESTAMP_MICROS(event_timestamp)), MIN(TIMESTAMP_MICROS(event_timestamp)), MILLISECOND) AS page_visit_time_msec,
  SUM(engagement_time_msec) AS engagement_time_msec,  
  FROM label l
  GROUP BY ALL 
),
-- ページ滞在時間, エンゲージメント時間
page_visit_time AS(
	SELECT user_pseudo_id, ga_session_id,page_location_canonicalize,event_date, hostname, 
	COUNT(DISTINCT event_timestamp) AS page_views,
	SUM(page_visit_time_msec) AS sum_page_visit_time_msec,
	SUM(v.engagement_time_msec) AS sum_engagement_time_msec,
  FROM label l LEFT JOIN visit_time v USING(event_date, user_pseudo_id, ga_session_id, page_location_canonicalize, page_number)
	WHERE event_name IN ('page_view','screen_view')
	GROUP BY ALL 
),
key_events AS(
	SELECT keys FROM UNNEST([${constants.GA4_ANALYSIS_CV_EVENTS.map(value => `'${value}'`).join(", ")}]) AS keys
),
-- 各キーイベントで最初のコンバージョンを取得
first_cv AS(
  SELECT DISTINCT event_date, user_pseudo_id, ga_session_id, event_name,event_timestamp --page_location_canonicalize
  FROM e
  WHERE event_name IN (SELECT keys FROM key_events)
  QUALIFY ROW_NUMBER()OVER(PARTITION BY user_pseudo_id, ga_session_id, event_name ORDER BY rn) = 1
),
-- CV貢献：各セッションの最初のページから各CVのセッション内最初のCVまでが対象。同一セッション内で複数回閲覧しても１としてカウントする。（対象CV：各CVの最初のCVまで。CVに到達するまでの全ページに+1（複数回ページビューがあっても1））（各キーイベントの最初のコンバージョンよりも前のユニークページを抽出）
contribute_conversion AS(
	SELECT user_pseudo_id, ga_session_id, event_date, hostname, page_location_canonicalize,
  ${constants.GA4_ANALYSIS_CV_EVENTS.map(value => `	COALESCE(${value},0) AS ${value},`).join("\n")}
	-- COALESCE(generate_lead,0) AS generate_lead, 
	-- COALESCE(sign_up,0) AS sign_up,
	-- COALESCE(download_form,0) AS download_form,
	-- COALESCE(contact_service_thanks,0) AS contact_service_thanks,
	-- COALESCE(file_download,0) AS file_download,
	FROM (
	  SELECT DISTINCT f.event_name, e.user_pseudo_id, e.ga_session_id, e.event_date, e.hostname, e.page_location_canonicalize, -- イベントが複数回発生しても1回としてカウント
	  FROM first_cv f LEFT JOIN e USING(user_pseudo_id, ga_session_id)
	  WHERE e.event_name IN('page_view','screen_view')
	  AND e.event_timestamp <= f.event_timestamp 
	)
	PIVOT (
	  COUNT(event_name) FOR event_name IN (${constants.GA4_ANALYSIS_CV_EVENTS.map(value => `'${value}'`).join(", ")}) -- ,'purchase','file_download','view_form_inquiry','view_form_sign_up')
	)
)
,all_views AS(
	SELECT user_pseudo_id, ga_session_id,page_location_canonicalize,event_date,hostname,
	pv.* EXCEPT(user_pseudo_id, ga_session_id,page_location_canonicalize,event_date,hostname),
	cc.* EXCEPT(user_pseudo_id, ga_session_id,page_location_canonicalize,event_date,hostname),
	FROM page_visit_time pv
	FULL JOIN contribute_conversion cc USING(user_pseudo_id, ga_session_id,page_location_canonicalize,event_date,hostname)
)
,result AS(
	SELECT a.page_location_canonicalize,a.hostname,a.event_date,
	SUM(page_views) AS page_views,
	COUNT(DISTINCT IF(page_views>0,CONCAT(user_pseudo_id,ga_session_id),NULL)) AS unique_page_views,
	SUM(sum_page_visit_time_msec) AS sum_page_visit_time_msec,
	SUM(sum_engagement_time_msec) AS sum_engagement_time_msec,
    ${constants.GA4_ANALYSIS_CV_EVENTS.map(value => `COUNT(DISTINCT IF(${value}>0,CONCAT(user_pseudo_id,ga_session_id),NULL)) AS attributeEvents_${value},`).join("\n	")}
	-- COUNT(DISTINCT IF(generate_lead>0,CONCAT(user_pseudo_id,ga_session_id),NULL)) AS generate_lead,
	-- COUNT(DISTINCT IF(sign_up>0,CONCAT(user_pseudo_id,ga_session_id),NULL)) AS sign_up,
	-- COUNT(DISTINCT IF(download_form>0,CONCAT(user_pseudo_id,ga_session_id),NULL)) AS download_form,
	-- COUNT(DISTINCT IF(contact_service_thanks>0,CONCAT(user_pseudo_id,ga_session_id),NULL)) AS contact_service_thanks,
	-- COUNT(DISTINCT IF(file_download>0,CONCAT(user_pseudo_id,ga_session_id),NULL)) AS file_download,
	s.* EXCEPT(user_pseudo_id, ga_session_id,event_date)
	FROM all_views a
	LEFT JOIN s USING(user_pseudo_id, ga_session_id,event_date)
	GROUP BY 
		page_location_canonicalize,
		event_date,
		hostname,
		user_traffic_source,
		user_traffic_medium,
		user_traffic_campaign,
		session_traffic_source,
		session_traffic_medium,
		session_traffic_campaign,
		session_traffic_campaign_id,
		session_traffic_content,
		session_traffic_term,
		session_traffic_source_platform,
		session_traffic_creative_format,
		session_traffic_marketing_tactic,
		platform,
		device_category,
		operating_system,
		browser,
		session_traffic_source_last_click
)
SELECT *
FROM result








