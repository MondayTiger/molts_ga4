/* 
  CV分析ビュー
  対象イベントが発生した回数、発生したイベント数を参照元別などで集計
  対象イベント名：sign_up,  download_form,contact_service_thanks ※constants.jsにて設定
*/

-- Config blocks
config {
  type: "view",
  schema: constants.REPORT,
  description: "GA4 CV分析ビュー",
  tags: ["report", "analysis"],
  columns: {

    event_date: "日付",
    generate_lead:"キーイベント：generate_leadが発生した回数",
    sign_up:"キーイベント：sign_upが発生した回数",
    download_form:"キーイベント：download_formが発生した回数",
    contact_service_thanks:"キーイベント：contact_service_thanksが発生した回数",
    file_download:"キーイベント：file_downloadが発生した回数",
    generate_lead_sessions:"キーイベント：generate_leadが発生したセッション数",
    sign_up_sessions:"キーイベント：sign_upが発生したセッション数",
    download_form_sessions:"キーイベント：download_formが発生したセッション数",
    contact_service_thanks_sessions:"キーイベント：contact_service_thanksが発生したセッション数",
    file_download_sessions:"キーイベント：file_downloadが発生したセッション数",
    user_traffic_source: "集客：ユーザー獲得参照元",
    user_traffic_medium: "集客：ユーザー獲得メディア",
    user_traffic_campaign: "集客：ユーザー獲得キャンペーン",
    session_traffic_source: "集客：セッションの参照元",
    session_traffic_medium: "集客：セッションのメディア",
    session_traffic_campaign: "集客：セッションのキャンペーン",
    session_traffic_campaign_id: "集客：セッションのキャンペーンID（utm_id）",
    session_traffic_content: "集客：セッションの広告のコンテンツ",
    session_traffic_term: "集客：セッションの検索キーワード",
    session_traffic_source_platform: "集客：セッションの参照元プラットフォーム",
    session_traffic_creative_format: "集客：セッションのクリエイティブのタイプ",
    session_traffic_marketing_tactic: "集客：セッションのターゲティング条件",
    platform: "プラットフォーム",
    device_category: "デバイス カテゴリ",
    operating_system: "OS",
    browser: "ブラウザ",
    browser_version: "ブラウザのバージョン",
    geo_continent: "大陸",
    geo_country: "国",
    geo_region: "地域",
    geo_city: "市区町村",
    geo_sub_continent: "亜大陸",
    geo_metro: "大都市圏",
    hostname: "ホスト名",
    page_location_canonicalize: "正規化URL",
    // entrance_page: "セッション内の閲覧開始ページ",
    // exit_page: "セッション内の離脱ページ",
    // session_traffic_source_last_clickの参照元

    session_traffic_source_last_click: {
      description: "集客：セッションの参照元情報",
      columns: {
        manual_campaign: {
          description: "Description of the manual_campaign RECORD",
          columns: {
            manual_campaign: "集客：セッションのキャンペーン",
            campaign_id: "集客：セッションのキャンペーンID（utm_id）",
            campaign_name: "集客：セッションのキャンペーン",
            source: "集客：セッションの参照元",
            medium: "集客：セッションのメディア",
            term: "集客：セッションの検索キーワード",
            content: "集客：セッションの広告のコンテンツ",
            source_platform: "集客：セッションの参照元プラットフォーム",
            creative_format: "集客：セッションのクリエイティブのタイプ",
            marketing_tactic: "集客：セッションのターゲティング条件"
          }
        },
        google_ads_campaign: {
          description: "Google広告関連",
          columns: {
            customer_id: "Google 広告アカウントに関連付けられた顧客 ID",
            account_name: "Google広告のアカウント名",
            campaign_id: "Google広告キャンペーンID",
            campaign_name: "Google広告キャンペーン名",
            ad_group_id: "Google広告キャンペーン内の広告グループのID",
            ad_group_name: "Google広告キャンペーン内の広告グループ名"
          }
        }
      }
    }
  }
}

-- Query blocks
with date_range AS(
  SELECT
    ${constants.GA4_ANALYSIS_CV_START_DATE} AS start_date,
    ${constants.GA4_ANALYSIS_CV_END_DATE} AS end_date
),
s AS(
	SELECT DISTINCT
	event_date,
	user_pseudo_id,
	ga_session_id,	
	-- user_id,
	-- stream_id,
	user_traffic_source,
	user_traffic_medium,
	user_traffic_campaign,
--	ga_session_number,
	session_traffic_source,
	session_traffic_medium,
	session_traffic_campaign,
	session_traffic_campaign_id,
	session_traffic_content,
	session_traffic_term,
	session_traffic_source_platform,
	session_traffic_creative_format,
	session_traffic_marketing_tactic,
	platform,
	device_category,
	-- device_mobile_brand_name,
	-- device_mobile_model_name,
	-- device_mobile_marketing_name,
	-- device_mobile_os_hardware_model,
	operating_system,
	-- operating_system_version,
	-- device_vendor_id,
	-- device_advertising_id,
	-- language,
	browser,
	-- browser_version,
	-- geo_continent,
	-- geo_country,
	-- geo_region,
	-- geo_city,
	-- geo_sub_continent,
	-- geo_metro,
	session_traffic_source_last_click,
	FROM ${ref("m_ga4_session")}
  WHERE event_date BETWEEN (SELECT start_date FROM date_range) AND (SELECT end_date FROM date_range)  
	QUALIFY ROW_NUMBER()OVER(PARTITION BY event_date, user_pseudo_id,	ga_session_id ORDER BY entrance_timestamp) = 1 
),
e AS(
 SELECT DISTINCT 
	event_date,
	user_pseudo_id,
	ga_session_id,

	hostname,
	page_location_canonicalize,
	event_timestamp,
	event_timestamp_jst,
	event_name, 
	engagement_time_msec,
	COALESCE(batch_page_id,0) AS batch_page_id,
	COALESCE(batch_ordering_id, 0) AS batch_ordering_id,
	COALESCE(batch_event_index,0) AS batch_event_index,
	ROW_NUMBER()OVER(PARTITION BY user_pseudo_id, ga_session_id ORDER BY COALESCE(batch_page_id,0),COALESCE(batch_ordering_id, 0),event_timestamp, COALESCE(batch_event_index,0)) AS rn

  -- 0 AS batch_page_id,
  -- 0 AS batch_ordering_id,
  -- 0 AS batch_event_index,
	-- ROW_NUMBER()OVER(PARTITION BY user_pseudo_id, ga_session_id ORDER BY event_timestamp,CASE WHEN event_name='purchase'THEN 3 WHEN event_name IN (${constants.GA4_ANALYSIS_CV_EVENTS.map(value => `'${value}'`).join(", ")}) THEN 2 ELSE 1 END) AS rn
  FROM ${ref("m_ga4_event")}
  WHERE event_date BETWEEN (SELECT start_date FROM date_range) AND (SELECT end_date FROM date_range)  
),
cv_event AS(
    SELECT *
    FROM (
        SELECT event_date, user_pseudo_id, ga_session_id, event_name
        FROM  e
        WHERE event_date BETWEEN (SELECT start_date FROM date_range) AND (SELECT end_date FROM date_range)  
            AND event_name IN (${constants.GA4_ANALYSIS_CV_EVENTS.map(value => `'${value}'`).join(", ")})
    )            
    PIVOT(
    COUNT(event_name) FOR event_name IN (${constants.GA4_ANALYSIS_CV_EVENTS.map(value => `'${value}'`).join(", ")})
    )
)
SELECT ce.event_date,
${constants.GA4_ANALYSIS_CV_EVENTS.map(value => `SUM(${value}) AS ${value},`).join("\n")}
${constants.GA4_ANALYSIS_CV_EVENTS.map(value => `COUNTIF(${value}>0) AS ${value}_sessions,`).join("\n")}
s.* EXCEPT(user_pseudo_id, ga_session_id,event_date)
FROM cv_event ce
LEFT JOIN s USING(event_date, user_pseudo_id, ga_session_id)
GROUP BY 
    event_date,
    user_traffic_source,
    user_traffic_medium,
    user_traffic_campaign,
    session_traffic_source,
    session_traffic_medium,
    session_traffic_campaign,
    session_traffic_campaign_id,
    session_traffic_content,
    session_traffic_term,
    session_traffic_source_platform,
    session_traffic_creative_format,
    session_traffic_marketing_tactic,
    platform,
    device_category,
    operating_system,
    browser,
    session_traffic_source_last_click


