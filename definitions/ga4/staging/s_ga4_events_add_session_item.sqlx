-- Config blocks
config {
    type: "view",
    schema: "df_molts_ga4_staging_new",
    description: "GA4ステージングデータ セッションデータを追加",
    tags: ["daily", "staging"],
    columns: {
        // timestamp
        event_date: "日付",
        event_timestamp: "タイムスタンプ（マイクロ秒）",
        event_timestamp_jst: "タイムスタンプ_日本時間",
        entrance_timestamp: "タイムスタンプ（セッション開始）（マイクロ秒）",
        entrance_timestamp_jst: "タイムスタンプ（セッション開始）_日本時間",
        exit_timestamp: "タイムスタンプ（離脱）（マイクロ秒）",
        exit_timestamp_jst: "タイムスタンプ（離脱）_日本時間",

        // event
        event_name: "イベント名",
        event_bundle_sequence_id: "これらのイベントをまとめたアップロード用バンドルのシーケンシャル ID",

        // stream / platform
        stream_id: "ストリームID",
        platform: "プラットフォーム",

        // user
        user_id: "ユーザーID",
        user_pseudo_id: "ユニークユーザ識別用ID",
        user_first_touch_timestamp: "ユーザーが初めてアプリを起動したか、サイトに訪れた時刻（マイクロ秒単位）",

        // user_acquistion_channel
        user_traffic_source: "集客：ユーザー獲得参照元",
        user_traffic_medium: "集客：ユーザー獲得メディア",
        user_traffic_campaign: "集客：ユーザー獲得キャンペーン",

        // session
        ga_session_id: "セッションID",
        ga_session_number: "通算セッション数",
        session_duration: "セッション時間（秒）",
        session_total_engagement_time: "セッション合計エンゲージメント時間（秒）",
        session_total_page_view: "セッション合計ページビュー",
        session_total_screen_view: "セッション合計スクリーンビュー",
        is_bounce_no_transition: "直帰_遷移なし",
        is_bounce_no_engagement: "直帰_エンゲージメントなし",

        // event_acquisition_channel
        event_traffic_source: "集客：イベントの参照元",
        event_traffic_medium: "集客：イベントのメディア",
        event_traffic_campaign: "集客：イベントのキャンペーン",
        event_traffic_content: "集客：イベントの広告のコンテンツ",
        event_traffic_term: "集客：イベントの検索キーワード",
        event_source_platform: "集客：イベントの参照元プラットフォーム",
        event_creative_format: "集客：イベントのクリエイティブのタイプ",
        event_marketing_tactic: "集客：イベントのターゲティング条件",
        event_traffic_campaign_id: "集客：イベントのキャンペーンID（utm_id）",
        event_traffic_gclid: "集客：イベントのgclid",

        // session_acquisition_channel
        session_traffic_source: "集客：セッションの参照元",
        session_traffic_medium: "集客：セッションのメディア",
        session_traffic_campaign: "集客：セッションのキャンペーン",
        session_traffic_content: "集客：セッションの広告のコンテンツ",
        session_traffic_term: "集客：セッションの検索キーワード",
        session_traffic_platform: "集客：セッションの参照元プラットフォーム",
        session_creative_format: "集客：セッションのクリエイティブのタイプ",
        session_marketing_tactic: "集客：セッションのターゲティング条件",
        session_traffic_campaign_id: "集客：セッションのキャンペーンID（utm_id）",
        session_traffic_gclid: "集客：セッションのgclid",

        // device
        device_category: "デバイス カテゴリ",
        device_mobile_brand_name: "デバイスのブランド名",
        device_mobile_model_name: "デバイスのモデル名",
        device_mobile_marketing_name: "デバイスのマーケティング名",
        device_mobile_os_hardware_model: "オペレーティング システムから直接取得したデバイスのモデル情報",
        operating_system: "OS",
        operating_system_version: "OSのバージョン",
        device_vendor_id: "IDFV（IDFA を収集していない場合にのみ使用）",
        device_advertising_id: "広告 ID または IDFA",
        language: "言語",
        is_limited_ad_tracking: "デバイスの広告トラッキング制限の設定 iOS14 以降では、IDFA がゼロ以外の場合、false が返される",
        time_zone_offset_seconds: "GMT との時差（秒単位）",
        browser: "ブラウザ",
        browser_version: "ブラウザのバージョン",

        // geo
        geo_continent: "大陸",
        geo_sub_continent: "亜大陸",
        geo_country: "国",
        geo_region: "地域",
        geo_metro: "大都市圏",
        geo_city: "市区町村",

        // privacy_info
        privacy_info_analytics_storage: "ユーザーに対してアナリティクスのデータ保存が有効になっているかどうか",
        privacy_info_ads_storage: "ユーザーに対して広告ターゲティングが有効になっているかどうか",
        privacy_info_uses_transient_token: "ウェブユーザーがアナリティクスでのデータ保存を拒否し、デベロッパーがサーバーデータの一時的なトークンに基づいて Cookie を使用しない測定を有効にしているかどうか",

        // page
        hostname: "ホスト名",
        page_location: "URL",
        page_location_canonicalize: "正規化URL",
        content_group: "コンテンツグループ",
        page_title: "ページタイトル",
        entrances: "閲覧開始フラグ(GA4の entrances イベントパラメータ)",
        entrance_page: "セッション内の閲覧開始ページ",
        exit_page: "セッション内の離脱ページ",
        flg_reload: "リロードフラグ",

        // referrer
        page_referrer: "リファラー",

        // scroll
        percent_scrolled: "スクロールの割合",

        // event_tracking (Click Tracking)
        event_category: "イベント カテゴリ",
        event_action: "イベント アクション",
        event_label: "イベント ラベル",
        link_id: "要素ID",
        link_classes: "要素クラス",
        link_domain: "リンク先ドメイン",
        link_url: "リンク先URL",
        link_url_canonicalize: "正規化リンク先URL",
        link_text: "リンクテキスト",
        outbound: "アウトバウンド",
        file_extension: "ファイルの拡張子",
        file_name: "ファイル名",

        // video
        video_title: "動画タイトル",
        video_provider: "動画プロバイダー",
        video_url: "動画URL",
        visible: "動画ビジブル",
        video_current_time: "動画の現在の時間",
        video_duration: "動画の長さ",
        video_percent: "動画再生率",

        // search_results
        search_term: "サイト内検索キーワード",

        // experiment (a/b testing)
        experiment_id: "A/BテストのテストID",
        variant_id: "A/BテストのバリエーションID",

        // engagement_time
        engagement_time_msec: "エンゲージメント時間（ミリ秒）",

        // traffic_type (excluding IP address)
        traffic_type: "トラフィックタイプ（関係者アクセス）",

        // debug_mode
        debug_mode: "デバッグモード",

        // add_column
        is_fixed_data: "FIXしたデータか",

        // event_parameter clarity
        claritydimension: "Microsoft ClarityのURL",

        // event_parameter article
        estimate_read_time: "推定読了時間",
        estimate_read_time_percent: "読了割合",

        // event_parameter core web vitals
        cwv_id: "Core Web VitalsのID",
        cwv_value: "Core Web Vitalsのvalue",
        cwv_delta: "Core Web Vitalsのdelta"
    }
}


-- Query blocks
-- 共通処理
WITH ga AS (
    SELECT
        *,

        -- Entrance/Exit Timestamp
        ${helpers.getFirstValue('event_timestamp','entrance_timestamp')},
        ${helpers.getLastValue('event_timestamp','exit_timestamp')},
        ${helpers.getFirstValue('event_timestamp_jst','entrance_timestamp_jst')},
        ${helpers.getLastValue('event_timestamp_jst','exit_timestamp_jst')},

        -- Landing/Exit Page
        ${helpers.getFirstValue('page_location','entrance_page')},
        ${helpers.getLastValue('page_location','exit_page')},

        -- traffic_source
        ${helpers.getFirstValue('event_traffic_campaign','session_traffic_campaign')},

    FROM
	    ${ref("s_ga4_events_event_update")}
),

-- 参照元の追加処理
agg_campaign AS (
    SELECT
        user_pseudo_id,
        ga_session_id,
        session_traffic_campaign,
    FROM
        ga
    WHERE
        event_traffic_campaign IS NOT NULL
    GROUP BY
        1,2,3
),

agg_campaign_first AS (
    SELECT
        g.user_pseudo_id,
        g.ga_session_id,

        -- session_acquisition_channel
        g.event_traffic_source AS session_traffic_source,
        g.event_traffic_medium AS session_traffic_medium,
        g.event_traffic_campaign AS session_traffic_campaign,
        g.event_traffic_content AS session_traffic_content,
        g.event_traffic_term AS session_traffic_term,
        g.event_source_platform AS session_traffic_platform,
        g.event_creative_format AS session_creative_format,
        g.event_marketing_tactic AS session_marketing_tactic,
        g.event_traffic_campaign_id AS session_traffic_campaign_id,
        g.event_traffic_gclid AS session_traffic_gclid,

        MIN(event_timestamp) -- 一番早いイベントに突合し、utm_campaignがかぶった場合にも正しい参照元に紐づける

    FROM
        ga AS g
        INNER JOIN agg_campaign AS a
            ON  g.user_pseudo_id = a.user_pseudo_id
            AND g.ga_session_id = a.ga_session_id
            AND g.event_traffic_campaign = a.session_traffic_campaign
    GROUP BY
        1,2,3,4,5,6,7,8,9,10,11,12
),

agg_campaign_first_2 AS (
    SELECT
        *,
        ROW_NUMBER() OVER(PARTITION BY user_pseudo_id, ga_session_id ORDER BY ga_session_id) AS row_num
    FROM
        agg_campaign_first
),

add_traffic_source AS (
    SELECT

        -- timestamp
        g.event_date,
        g.event_timestamp,
        g.event_timestamp_jst,
        g.entrance_timestamp,
        g.exit_timestamp,
        g.entrance_timestamp_jst,
        g.exit_timestamp_jst,

        -- event
        g.event_name,
        g.event_bundle_sequence_id,

        -- stream / platform
        g.stream_id,
        g.platform,

        -- user
        g.user_id,
        g.user_pseudo_id,
        g.user_first_touch_timestamp,

        -- user_acquistion_channel
        g.user_traffic_source,
        g.user_traffic_medium,
        g.user_traffic_campaign,

        -- session
        g.ga_session_id,
        g.ga_session_number,

        -- event_acquisition_channel
        g.event_traffic_source,
        g.event_traffic_medium,
        g.event_traffic_campaign,
        g.event_traffic_content,
        g.event_traffic_term,
        g.event_source_platform,
        g.event_creative_format,
        g.event_marketing_tactic,
        g.event_traffic_campaign_id, -- utm_id
        g.event_traffic_gclid,

        -- device
        g.device_category,
        g.device_mobile_brand_name,
        g.device_mobile_model_name,
        g.device_mobile_marketing_name,
        g.device_mobile_os_hardware_model,
        g.operating_system,
        g.operating_system_version,
        g.device_vendor_id,
        g.device_advertising_id,
        g.language,
        g.is_limited_ad_tracking,
        g.time_zone_offset_seconds,
        g.browser,
        g.browser_version,

        -- geo
        geo_continent,
        geo_sub_continent,
        geo_country,
        geo_region,
        geo_metro,
        geo_city,

        -- privacy_info
        g.privacy_info_analytics_storage,
        g.privacy_info_ads_storage,
        g.privacy_info_uses_transient_token,

        -- page
        g.hostname,
        g.page_location,
        g.page_location_canonicalize,
        g.content_group,
        g.page_title,
        g.entrances,
        g.entrance_page,
        g.exit_page,
        g.flg_reload, -- original

        -- referrer
        g.page_referrer,

        -- scroll
        g.percent_scrolled,

        -- event_tracking (Click Tracking)
        g.event_category,
        g.event_action,
        g.event_label,
        g.link_id,
        g.link_classes,
        g.link_domain,
        g.link_url,
        g.link_url_canonicalize,
        g.link_text,
        g.outbound,
        g.file_extension,
        g.file_name,

        -- video
        g.video_title,
        g.video_provider,
        g.video_url,
        g.visible,
        g.video_current_time,
        g.video_duration,
        g.video_percent,

        -- view_search_results
        g.search_term,

        -- experiment (a/b testing)
        experiment_id,
        variant_id,

        -- engagement_time_msec
        g.engagement_time_msec,

        -- traffic_type (excluding IP address)
        g.traffic_type,

        -- session_acquisition_channel
        a.session_traffic_source,
        a.session_traffic_medium,
        a.session_traffic_campaign,
        a.session_traffic_content,
        a.session_traffic_term,
        a.session_traffic_platform,
        a.session_creative_format,
        a.session_marketing_tactic,
        a.session_traffic_campaign_id,
        a.session_traffic_gclid,

        -- debug_mode
        g.debug_mode,

        -- add_column
        g.is_fixed_data,

        -- event_parameter clarity
        g.claritydimension,

        -- event_parameter article
        g.estimate_read_time,
        g.estimate_read_time_percent,

        -- event_parameter core web vitals
        g.cwv_id,
        g.cwv_value,
        g.cwv_delta,

        FROM
            ga AS g
            LEFT JOIN (SELECT * FROM agg_campaign_first_2 WHERE row_num = 1) AS a
                ON  g.user_pseudo_id = a.user_pseudo_id
                AND g.ga_session_id = a.ga_session_id
        
),

-- セッション時間の処理

calculate_session_duration AS (
    SELECT
	    user_pseudo_id,
	    ga_session_id,
	    (exit_timestamp - entrance_timestamp) / 1000000 AS session_duration
    FROM
        ga
    GROUP BY
        1,2,3
),

calculate_engagement_time AS (
    SELECT 
	    user_pseudo_id,
	    ga_session_id,
	    SUM(engagement_time_msec) / 1000 AS session_total_engagement_time
    FROM (
        -- engagementが生じていた時間のデータを抽出する
        SELECT 
            user_pseudo_id,
            ga_session_id,
            event_bundle_sequence_id, -- 一度に発生したイベントを1つにまとめるためのID
            engagement_time_msec,
        FROM 
            ga
        GROUP BY 
            1,2,3,4
    )
    GROUP BY
        1,2
),

-- 直帰の処理

calculate_sum_pv_sv AS (
    SELECT
        user_pseudo_id,
        ga_session_id,
        --page_viewとscreen_viewのイベントの発生回数を数える
        COUNT(CASE WHEN event_name = 'page_view' THEN event_name END) AS session_total_page_view,
        COUNT(CASE WHEN event_name = 'screen_view' THEN event_name END) AS session_total_screen_view
    FROM
        ga
    GROUP BY
        1,2
),

join_data AS (
    SELECT
        t.*,
        s.session_duration,
        e.session_total_engagement_time,
        v.session_total_page_view,
        v.session_total_screen_view,
        IF(v.session_total_page_view + v.session_total_screen_view <= 1, true, false) AS is_bounce_no_transition, --page_viewとscreen_viewの和が1以下のユーザーを直帰判定（true）とする
        CASE
            WHEN s.session_duration < 10 THEN true -- エンゲージメント秒数の定義。10秒を変更している場合は 10 を変更する。
            WHEN v.session_total_page_view + v.session_total_screen_view <= 1 THEN true
            ELSE false END AS is_bounce_no_engagement
    FROM
        add_traffic_source as t
    LEFT JOIN　calculate_session_duration as s
        ON　t.user_pseudo_id = s.user_pseudo_id
        AND t.ga_session_id = s.ga_session_id
    LEFT JOIN　calculate_engagement_time as e
        ON　t.user_pseudo_id = e.user_pseudo_id
        AND t.ga_session_id = e.ga_session_id
    LEFT JOIN calculate_sum_pv_sv AS v
        ON  t.user_pseudo_id = v.user_pseudo_id
        AND t.ga_session_id = v.ga_session_id
)

SELECT
    -- timestamp
    event_date,
    event_timestamp,
    event_timestamp_jst,
    entrance_timestamp,
    entrance_timestamp_jst,
    exit_timestamp,
    exit_timestamp_jst,

    -- event
    event_name,
    event_bundle_sequence_id,

    -- stream / platform
    stream_id,
    platform,

    -- user
    user_id,
    user_pseudo_id,
    ${helpers.getFirstValue('user_first_touch_timestamp','user_first_touch_timestamp')},

    -- user_acquistion_channel
    IFNULL(${helpers.getFirstValueNoColumnName('user_traffic_source')},"(direct)") AS user_traffic_source,
    IFNULL(${helpers.getFirstValueNoColumnName('user_traffic_medium')},"(none)") AS user_traffic_medium,
    IFNULL(${helpers.getFirstValueNoColumnName('user_traffic_campaign')},"(direct)") AS user_traffic_campaign,

    -- session
    ga_session_id,
    ga_session_number,
    session_duration,
    session_total_engagement_time,
    session_total_page_view,
    session_total_screen_view,
    is_bounce_no_transition,
    is_bounce_no_engagement,

    -- event_acquisition_channel
    event_traffic_source,
    event_traffic_medium,
    event_traffic_campaign,
    event_traffic_content,
    event_traffic_term,
    event_source_platform,
    event_creative_format,
    event_marketing_tactic,
    event_traffic_campaign_id,
    event_traffic_gclid,

    -- session_acquisition_channel
    IFNULL(session_traffic_source,"(direct)") AS session_traffic_source,
    IFNULL(session_traffic_medium,"(none)") AS session_traffic_medium,
    IFNULL(session_traffic_campaign,"(direct)") AS session_traffic_campaign,
    session_traffic_content,
    session_traffic_term,
    session_traffic_platform,
    session_creative_format,
    session_marketing_tactic,
    session_traffic_campaign_id,
    session_traffic_gclid,

    -- device
    device_category,
    device_mobile_brand_name,
    device_mobile_model_name,
    device_mobile_marketing_name,
    device_mobile_os_hardware_model,
    operating_system,
    operating_system_version,
    device_vendor_id,
    device_advertising_id,
    language,
    is_limited_ad_tracking,
    time_zone_offset_seconds,
    browser,
    browser_version,

    -- geo
    geo_continent,
    geo_country,
    geo_region,
    geo_city,
    geo_sub_continent,
    geo_metro,

    -- privacy_info
    privacy_info_analytics_storage,
    privacy_info_ads_storage,
    privacy_info_uses_transient_token,

    -- page
    hostname,
    page_location,
    page_location_canonicalize,
    content_group,
    page_title,
    entrances,
    entrance_page,
    exit_page,
    flg_reload, -- original

    -- referrer
    page_referrer,

    -- scroll
    percent_scrolled,

    -- event_tracking (Click Tracking)
    event_category,
    event_action,
    event_label,
    link_id,
    link_classes,
    link_domain,
    link_url,
    link_url_canonicalize,
    link_text,
    outbound,
    file_extension,
    file_name,

    -- video
    video_title,
    video_provider,
    video_url,
    visible,
    video_current_time,
    video_duration,
    video_percent,

    -- search_results
    search_term,

    -- experiment (a/b testing)
    experiment_id,
    variant_id,

    -- engagement_time
    engagement_time_msec,

    -- traffic_type (excluding IP address)
    traffic_type,

    -- debug_mode
    debug_mode,

    -- add_column
    is_fixed_data,

    -- event_parameter clarity
    claritydimension,

    -- event_parameter article
    estimate_read_time,
    estimate_read_time_percent,

    -- event_parameter core web vitals
    cwv_id,
    cwv_value,
    cwv_delta,

FROM
    join_data