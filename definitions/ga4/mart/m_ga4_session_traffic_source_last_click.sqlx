-- Config blocks
config {
    //typeは初期構築時は"table"、増分更新時は"incremental"とする。
    /* 初期構築時の方法
        1. typeをtableにする
        2. 最後のSELECT文を要変更
    */
    type: "incremental",
    bigquery: {
        // パーティションとクラスタリングを設定
        partitionBy: "event_date",
        clusterBy: ["user_pseudo_id", "ga_session_id"]
    },
    schema: "df_molts_ga4_mart",
    description: "GA4マートデータ　セッションデータのtraffic_source_last_click",
    dependencies: ["m_ga4_session_traffic_source_last_click_delete_unfixed"],
    tags: ["daily", "mart"],
    columns: {
        // timestamp
        event_date: "日付",
        // user
        user_pseudo_id: "ユニークユーザ識別用ID",

        // session
        ga_session_id: "セッションID",

        user_id: "セッション内の最初のユーザーID",

        session_traffic_source_last_click:{
            description: "集客：セッションの参照元情報",
            columns:{
                manual_campaign: {
                    description: "Description of the manual_campaign RECORD",
                    columns: {
                        manual_campaign: "集客：セッションのキャンペーン",
                        campaign_id: "集客：セッションのキャンペーンID（utm_id）",
                        campaign_name: "集客：セッションのキャンペーン",
                        source: "集客：セッションの参照元",
                        medium:"集客：セッションのメディア",
                        term: "集客：セッションの検索キーワード",
                        content: "集客：セッションの広告のコンテンツ",
                        source_platform: "集客：セッションの参照元プラットフォーム",
                        creative_format: "集客：セッションのクリエイティブのタイプ",
                        marketing_tactic: "集客：セッションのターゲティング条件"
                    }
                },
                google_ads_campaign: {
                    description: "Google広告関連",
                    columns: {
                        customer_id: "Google 広告アカウントに関連付けられた顧客 ID",
                        account_name: "Google広告のアカウント名",
                        campaign_id: "Google広告キャンペーンID",
                        campaign_name: "Google広告キャンペーン名",
                        ad_group_id: "Google広告キャンペーン内の広告グループのID",
                        ad_group_name: "Google広告キャンペーン内の広告グループ名"
                    } 
                }
            }
        },
        // add_column
        is_fixed_data: "FIXしたデータか"
    }
}


-- Query blocks
SELECT * FROM ${ref("s_ga4_session_traffic_source_last_click")}
 ${ when(incremental(), `WHERE event_date > (SELECT MAX(event_date) FROM ${self()})`)}

-- 初期作業時は上のクエリをコメントアウトしてこちらを実行
-- SELECT * FROM ${ref("s_ga4_session_traffic_source_last_click_initial")}



