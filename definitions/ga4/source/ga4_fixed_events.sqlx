/*
 2023/11/24 コンパイル後のクエリで動作確認済み
 2024/07/31 参照元情報の取得方法を修正。
 2024/08/26 unique_key（イベントを一意に定める）を追加
*/
/*
初期実行時（過去分のデータを取り込む時は、最後のWHERE句を変更（デフォルト：7日前～5日前 → 5日前まで）
*/
-- Config blocks
config {
    type: "view",
    schema: "df_molts_ga4_source",
    description: "TEST GA4データソース 確定データ",
    tags: ["daily", "source"]
}


-- Query blocks
with ga AS(
SELECT

    -- timestamp
    event_date,
    event_timestamp,

    -- event
    event_name,
    event_bundle_sequence_id,

    -- stream / platform
    stream_id,
    platform,

    -- user
    COALESCE(user_id, ${helpers.getUserPropertyNoColumnName('user_id','string')}, ${helpers.getEventParamNoColumnName('user_id','string')}) AS user_id,
    user_pseudo_id,
    user_first_touch_timestamp,

    -- user_acquistion_channel
    traffic_source.source AS user_traffic_source,
    traffic_source.medium AS user_traffic_medium,
    traffic_source.name AS user_traffic_campaign,

    -- session
    ${helpers.getEventParamAll('ga_session_id','string')},
    ${helpers.getEventParam('ga_session_number','int')},
    ${helpers.getEventParamAll('session_engaged','int')},

    -- event_acquisition_channel
   -- 2023/12/1 以降のテーブルに関してはここから
   -- collected_traffic_sourceから参照元などを取得。ない場合はイベントパラメータから取得 
    COALESCE(collected_traffic_source.manual_source, ${helpers.getEventParamNoColumnName('source','string')}) AS event_traffic_source,
    COALESCE(collected_traffic_source.manual_medium, ${helpers.getEventParamNoColumnName('medium','string')}) AS event_traffic_medium,
    COALESCE(collected_traffic_source.manual_campaign_name, ${helpers.getEventParamNoColumnName('campaign','string')}) AS event_traffic_campaign,
    COALESCE(collected_traffic_source.manual_campaign_id, ${helpers.getEventParamNoColumnName('campaign_id','string')}) AS event_traffic_campaign_id,
    COALESCE(collected_traffic_source.manual_content, ${helpers.getEventParamNoColumnName('content','string')}) AS event_traffic_content,
    COALESCE(collected_traffic_source.manual_term, ${helpers.getEventParamNoColumnName('term','string')}) AS event_traffic_term,
    COALESCE(collected_traffic_source.manual_source_platform, ${helpers.getEventParamNoColumnName('source_platform','string')}) AS event_traffic_source_platform,
    COALESCE(collected_traffic_source.manual_creative_format, ${helpers.getEventParamNoColumnName('creative_format','string')}) AS event_traffic_creative_format,
    COALESCE(collected_traffic_source.manual_marketing_tactic, ${helpers.getEventParamNoColumnName('marketing_tactic','string')}) AS event_traffic_marketing_tactic,
    COALESCE(collected_traffic_source.gclid, ${helpers.getEventParamNoColumnName('gclid','string')}) AS event_traffic_gclid,
    
/*
    COALESCE(collected_traffic_source.srsltid, ${helpers.getEventParamNoColumnName('srsltid','string')}) AS event_traffic_gclid, -- Google Merchant Center ID
    COALESCE(collected_traffic_source.dclid, ${helpers.getEventParamNoColumnName('dclid','string')}) AS event_traffic_dclid, -- 拡張アトリビューション
*/
    collected_traffic_source,
    session_traffic_source_last_click,
/*
    session_traffic_source_last_click.manual_campaign.campaign_id AS session_traffic_source_last_click_campaign_id,
    session_traffic_source_last_click.manual_campaign.campaign_name AS session_traffic_source_last_click_campaign_name,
    session_traffic_source_last_click.manual_campaign.source AS session_traffic_source_last_click_source,
    session_traffic_source_last_click.manual_campaign.medium AS session_traffic_source_last_click_medium,
    session_traffic_source_last_click.manual_campaign.term AS session_traffic_source_last_click_term,
    session_traffic_source_last_click.manual_campaign.content AS session_traffic_source_last_click_content,
    session_traffic_source_last_click.manual_campaign.source_platform AS session_traffic_source_last_click_source_platform,
    session_traffic_source_last_click.manual_campaign.creative_format AS session_traffic_source_last_click_creative_format,
    session_traffic_source_last_click.manual_campaign.marketing_tactic AS session_traffic_source_last_click_marketing_tactic,

    session_traffic_source_last_click.google_ads_campaign.customer_id AS google_ads_campaign_customer_id,
    session_traffic_source_last_click.google_ads_campaign.account_name AS google_ads_campaign_account_name,
    session_traffic_source_last_click.google_ads_campaign.campaign_id AS google_ads_campaign_campaign_id,
    session_traffic_source_last_click.google_ads_campaign.campaign_name AS google_ads_campaign_campaign_name,
    session_traffic_source_last_click.google_ads_campaign.ad_group_id AS google_ads_campaign_ad_group_id,
    session_traffic_source_last_click.google_ads_campaign.ad_group_name AS google_ads_campaign_ad_group_name,
*/
   -- 2023/12/1 以降のテーブルに関してはここまで

   -- 2023/11/30 までのテーブルに関してはここから
/* -- event_acquisition_channel -- 旧パターン
    ${helpers.getEventParamAll('source','string','event_traffic_source')},
    ${helpers.getEventParamAll('medium','string','event_traffic_medium')},
    ${helpers.getEventParamAll('campaign','string','event_traffic_campaign')},
    ${helpers.getEventParamAll('content','string','event_traffic_content')},
    ${helpers.getEventParamAll('term','string','event_traffic_term')},
    ${helpers.getEventParamAll('campaign_id','string','event_traffic_campaign_id')}, -- utm_id
    ${helpers.getEventParamAll('gclid','string','event_traffic_gclid')},
*/
   -- 2023/11/30 までのテーブルに関してはここまで
 
    -- device
    device.category AS device_category,
    device.mobile_brand_name AS device_mobile_brand_name,
    device.mobile_model_name AS device_mobile_model_name,
    device.mobile_marketing_name AS device_mobile_marketing_name,
    device.mobile_os_hardware_model AS device_mobile_os_hardware_model,
    device.operating_system,
    device.operating_system_version,
    device.vendor_id AS device_vendor_id,
    device.advertising_id AS device_advertising_id,
    device.language,
    device.is_limited_ad_tracking AS is_limited_ad_tracking,
    device.time_zone_offset_seconds AS time_zone_offset_seconds,
    device.web_info.browser,
    device.web_info.browser_version AS browser_version,

    -- geo
    geo.continent AS geo_continent,
    geo.sub_continent AS geo_sub_continent,
    geo.country AS geo_country,
    geo.region AS geo_region,
    geo.metro AS geo_metro,
    geo.city AS geo_city,

    -- privacy_info
    privacy_info.analytics_storage AS privacy_info_analytics_storage,
    privacy_info.ads_storage AS privacy_info_ads_storage,
    privacy_info.uses_transient_token AS privacy_info_uses_transient_token,

    -- page
    device.web_info.hostname,
    ${helpers.getEventParam('page_location')},
    ${helpers.getEventParamAll('content_group','string')},
    ${helpers.getEventParamAll('page_title','string')},
    ${helpers.getEventParam('entrances','int')},
    ${helpers.getEventParam('flg_reload')}, -- original
    ${helpers.getEventParam('screen_class')},
    ${helpers.getEventParam('firebase_screen_class')},
    ${helpers.getEventParam('screen_name')},
    

    -- referrer
    ${helpers.getEventParam('page_referrer')},

    -- scroll
    ${helpers.getEventParam('percent_scrolled','int')},

    -- event_tracking (Click Tracking)
    ${helpers.getEventParamAll('event_category','string')},
    ${helpers.getEventParamAll('event_action','string')},
    ${helpers.getEventParamAll('event_label','string')},
    ${helpers.getEventParamAll('link_id','string')},
    ${helpers.getEventParamAll('link_classes','string')},
    ${helpers.getEventParam('link_domain')},
    ${helpers.getEventParam('link_url')},
    ${helpers.getEventParamAll('link_text','string')},
    ${helpers.getEventParam('outbound')},
    ${helpers.getEventParam('file_extension')},
    ${helpers.getEventParamAll('file_name','string')},

    -- video
    ${helpers.getEventParamAll('video_title','string')},
    ${helpers.getEventParamAll('video_provider','string')},
    ${helpers.getEventParam('video_url')},
    ${helpers.getEventParam('visible')},
    ${helpers.getEventParam('video_current_time','int')},
    ${helpers.getEventParam('video_duration','int')},
    ${helpers.getEventParam('video_percent','int')},

    -- search_results
    ${helpers.getEventParamAll('search_term','string')},

    -- experiment (a/b testing)
    ${helpers.getEventParamAll('experiment_id','string')},
    ${helpers.getEventParamAll('variant_id','string')},

    -- engagement_time
    ${helpers.getEventParam('engagement_time_msec','int')},

    -- traffic_type (excluding IP address)
    ${helpers.getEventParamAll('traffic_type','string')},

    -- debug_mode
    ${helpers.getEventParam('debug_mode')},
    ${helpers.getEventParam('debug_event')},

    -- event_parameter clarity
    ${helpers.getEventParamAll('claritydimension','string')},

    -- event_parameter article
    ${helpers.getEventParamAll('estimate_read_time','int')},
    ${helpers.getEventParamAll('estimate_read_time_percent','int')},

    -- event_parameter core web vitals
    ${helpers.getEventParamAll('cwv_id','string')},
    ${helpers.getEventParamAll('cwv_value','double')},
    ${helpers.getEventParamAll('cwv_delta','double')},

    -- batch
    COALESCE(batch_page_id, ${helpers.getEventParamNoColumnName('batch_page_id','int')}) AS batch_page_id,
    COALESCE(batch_ordering_id, ${helpers.getEventParamNoColumnName('batch_ordering_id','int')}) AS batch_ordering_id,
    COALESCE(batch_event_index, ${helpers.getEventParamNoColumnName('batch_event_index','int')}) AS batch_event_index,

    -- common
    ${helpers.getEventParamAll('value','double')},
    ${helpers.getEventParamAll('method','string')}, 

    -- view_promotion, select_promotion    
    COALESCE(${helpers.getEventParamNoColumnName('promotion_id','string')}, (SELECT items[SAFE_OFFSET(0)].promotion_id)) AS promotion_id,
    COALESCE(${helpers.getEventParamNoColumnName('promotion_name','string')}, (SELECT items[SAFE_OFFSET(0)].promotion_name)) AS promotion_name,
    COALESCE(${helpers.getEventParamNoColumnName('creative_slot','string')}, (SELECT items[SAFE_OFFSET(0)].creative_slot)) AS creative_slot,
    COALESCE(${helpers.getEventParamNoColumnName('creative_name','string')}, (SELECT items[SAFE_OFFSET(0)].creative_name)) AS creative_name,
    -- purchase
    ecommerce.transaction_id AS transaction_id,
    COALESCE((SELECT items[SAFE_OFFSET(0)].item_id),'item_id') AS item_id,
    COALESCE(${helpers.getEventParamNoColumnName('item_list_id','string')},(SELECT items[SAFE_OFFSET(0)].item_list_id),'item_list_id') AS item_list_id,
    COALESCE(${helpers.getEventParamNoColumnName('item_list_name','string')}, (SELECT items[SAFE_OFFSET(0)].item_list_name),'item_list_name') AS item_list_name,

    -- event_parameter
    -- ${helpers.getEventParamAll('event_parameter_name','string')},
    -- ${helpers.getEventParamAll('event_parameter_name','int')},
    -- ${helpers.getEventParamAll('event_parameter_name','double')},

    -- user_property
    -- ${helpers.getUserPropertyAll('user_property_name','string')},
    -- ${helpers.getUserPropertyAll('user_property_name','int')},
    -- ${helpers.getUserPropertyAll('user_property_name','double')},

FROM                    
	${ref(constants.GA4_TABLE)}
WHERE
    _table_suffix BETWEEN FORMAT_DATE("%Y%m%d", DATE_SUB(CURRENT_DATE('Asia/Tokyo'), INTERVAL 7 DAY)) AND FORMAT_DATE("%Y%m%d", DATE_SUB(CURRENT_DATE('Asia/Tokyo'), INTERVAL 5 DAY))
--    _table_suffix BETWEEN '20201104' AND FORMAT_DATE("%Y%m%d", DATE_SUB(CURRENT_DATE('Asia/Tokyo'), INTERVAL 5 DAY))  -- 初回実行時に実施

)
-- unique_key：イベントを一意に定めるためのカラム※GAのバグやイベントの重複発火などにより全く同じイベントが発生した場合は同一の値となってしまいます（ですがm_ga4_eventテーブルではevent_indexと合わせてに使うことで一意になります）
SELECT *,
   CONCAT(user_pseudo_id, ga_session_id, event_timestamp, COALESCE(batch_page_id,-1),COALESCE(batch_ordering_id,-1),COALESCE(batch_event_index,-1), event_name,
    CONCAT(COALESCE(page_location,firebase_screen_class,screen_class,'page_location'),COALESCE(page_title,screen_name,'page_titile')), COALESCE(engagement_time_msec,0),
    COALESCE(
        CASE WHEN event_name IN('page_view','screen_view') THEN CONCAT(COALESCE(page_location,firebase_screen_class,screen_class,'page_location'),COALESCE(page_title,screen_name,'page_titile')),
        WHEN event_name LIKE 'log%' THEN method -- login, logoutなど
        WHEN event_name LIKE 'sign%' THEN method -- signin, sign_in など
        WHEN event_name LIKE '%click%' THEN COALESCE(link_url,link_classes) -- click, link_clickなど
        WHEN event_name LIKE 'scroll%' THEN SAFE_CAST(percent_scrolled AS STRING) -- scroll, scroll_pageなど
        WHEN event_name IN ('purcahse','refund') THEN transaction_id
        WHEN event_name LIKE '%_promotion' THEN CONCAT(promotion_name, promotion_id, creative_slot, creative_name) -- view_promotion, select_promotionなど
        WHEN event_name LIKE 'view_item' THEN item_id
        WHEN event_name LIKE 'view_item_list' THEN CONCAT(item_list_id, item_list_name, item_id)
        WHEN event_name IN ('add_to_cart','remove_from_cart','add_to_wishlist') THEN item_id
        WHEN event_name IN ('begin_checkout','add_payment_info','add_shipping_info') THEN SAFE_CAST(COALESCE(value,engagement_time_msec,0) AS STRING)
        -- 他にイベントがある場合はここに追加
        -- 例： WHEN event_name IN ('new_event') THEN event_parameter_name -- event_parameter_name はnew_eventイベントで使用されるパラメータ名を使用 ※218行目より上でevent_parameter_nameを作成する必要あり
        ELSE SAFE_CAST(COALESCE(engagement_time_msec,0) AS STRING)
        END, 'key') 
  ) AS unique_key
FROM ga


